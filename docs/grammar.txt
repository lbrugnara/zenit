program = declaration* ( ';' )?

declaration = attribute_declaration* ( variable_declaration | struct_declaration ) | statement ;

attribute_declaration = '#' '[' ID ( '(' ID ':' expression ( ',' ID ':' expression )*  ')' )? ']' ;

struct_declaration = 'struct' ID '{' struct_field_declaration+ '}' ;

struct_field_declaration = ID ':' type_information ';' ;

variable_declaration = 'var' ID ( ':' type_information )? '=' expression ';' ;

statement = expression_statement | if_statement | block ;

if_statement = 'if' '(' expression ')' block ( 'else' ( if_statement | block ) ) ;

block = '{' declaration*  '}' ;

expression_statement = expression ';' ;

expression = unary_expression ;

unary_expression = cast_expression | array_literal | reference_expression | identifier_expression | literal_expression ;

cast_expression = 'cast' '(' expression ( ':' type_information )? ')' ;

identifier_expression = named_struct_literal | identifier ;

named_struct_literal = ID struct_literal ;

struct_literal = '{' struct_field ( ',' struct_field )* '}' ;

struct_field = ID ':' expression ;

identifier = ID ;

reference_expression = '&' expression ;

array_literal = '[' ( expression ( ',' expression )* ','? )? ']' ;

literal_expression = integer_literal | boolean_literal ;

boolean_literal = BOOL ;

integer_literal = unsigned_integer ;

unsigned_integer = ( [0-9]+ | '0x' [a-fA-F0-9]+ | '0b' [0-1_]+ )

type_information = ( '[' integer_literal ']' )? '&'? ID ;
